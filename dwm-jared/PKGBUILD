_pkgname=dwm
pkgname=$_pkgname-jared
pkgver=6.2.r0.gcb3f58a
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'ttf-dejavu')
makedepends=('git')
#install=dwm.install
provides=('dwm')
conflicts=('dwm')
source=(dwm.desktop::https://aur.archlinux.org/cgit/aur.git/tree/dwm.desktop?h=dwm-git
        "$_pkgname::git+http://git.suckless.org/dwm"
        https://raw.githubusercontent.com/jaredforrest/PKGBUILDS/master/dwm-jared/dwm-jared.patch)
md5sums=('939f403a71b6e85261d09fc3412269ee'
         'SKIP'
         'SKIP')

pkgver(){
  cd $_pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
  patch --forward --strip=1 --input="${srcdir}/dwm-jared.patch"
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
  install -m644 -D ../dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
}

