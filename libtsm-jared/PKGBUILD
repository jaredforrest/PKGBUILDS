# Maintainer: Aetf <aetf at unlimitedcodeworks dor xyz>
_pkgname=libtsm
pkgname=$_pkgname-jared
pkgver=4.0.1.r3.g8d299ab
pkgrel=1
pkgdesc="Terminal-emulator State Machine. Patched flavor (using patches from http://github.com/Aetf/libtsm)"
arch=('x86_64' 'armv7h')
url="http://www.freedesktop.org/wiki/Software/kmscon/$_gitname"
license=('MIT')
depends=(glibc)
makedepends=('cmake' 'git' 'libxkbcommon')
provides=('libtsm-patched' 'libtsm')
conflicts=('libtsm' 'libtsm-patched')
options=(!libtool)
source=('git+https://github.com/Aetf/libtsm.git'
        'https://raw.githubusercontent.com/jaredforrest/PKGBUILDS/master/libtsm-jared/libtsm-jared.patch')
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long | sed -r "s/^$_pkgname-//;s/^v//;s/([^-]*-g)/r\\1/;s/-/./g"
}

prepare() {
  cd "$srcdir/$_pkgname"
  patch --forward --strip=1 --input="${srcdir}/libtsm-jared.patch"
      
  mkdir -p build && cd build

  cmake \
    -DBUILD_TESTING=OFF \
    -DBUILD_GTKTSM=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..
}

build() {
  cd "$srcdir/$_pkgname/build"
  make
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"

  cd build
  make DESTDIR="$pkgdir/" install
}
