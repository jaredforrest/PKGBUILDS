# Maintainer: Samantha Baldwin <fuhsaz 'plus' aur 'at' cryptic 'dot' li>
_pkgname=tabbed
pkgname=$_pkgname-jared
pkgver=0.6.29.gbc23614
pkgrel=1
pkgdesc="Simple generic tabbed fronted to xembed aware applications"
arch=('i686' 'x86_64')
url="http://tools.suckless.org/tabbed/"
license=('MIT/X')
depends=('libxft')
makedepends=('git')
provides=('tabbed')
conflicts=('tabbed')
source=("$_pkgname::git+http://git.suckless.org/tabbed"
	      https://raw.githubusercontent.com/jaredforrest/PKGBUILDS/tabbed-jared/tabbed-jared/tabbed-keyrelease-20191216-b5f9ec6.diff
	      https://raw.githubusercontent.com/jaredforrest/PKGBUILDS/tabbed-jared/tabbed-jared/tabbed-hidetabs-20191216-b5f9ec6.diff
	      https://raw.githubusercontent.com/jaredforrest/PKGBUILDS/tabbed-jared/tabbed-jared/alpha.diff)
md5sums=('SKIP'
	 'SKIP'
	 'SKIP'
	 'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's+[-_]+.+g'
}

prepare() {
    cd $_pkgname
    patch --forward --strip=1 --input="${srcdir}/tabbed-keyrelease-20191216-b5f9ec6.diff"
    patch --forward --strip=1 --input="${srcdir}/tabbed-hidetabs-20191216-b5f9ec6.diff"
    patch --forward --strip=1 --input="${srcdir}/alpha.diff"
}

build() {
  cd $_pkgname
  make PREFIX=/usr
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  install -Dm0644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
